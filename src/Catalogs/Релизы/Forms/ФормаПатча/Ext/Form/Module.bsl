
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Родитель) Тогда
		ЗаполнитьПоляДляПатча();
	КонецЕсли;
	
	ОбновитьЭлементы();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ОбновитьЭлементы();
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементы()
	ЭтоПатч = ЗначениеЗаполнено(Объект.Родитель);
	Если ЭтоПатч Тогда
		Элементы.Основное.ТекущаяСтраница = Элементы.Патч;
	Иначе
		Элементы.Основное.ТекущаяСтраница = Элементы.Релиз;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПоляДляПатча()
	Если Не ЗначениеЗаполнено(Объект.Родитель) Тогда
		Возврат;	
	КонецЕсли;
	
	НомерПоследнегоПатча = ПолучитьНомерПоследнегоПатча();
	
	РазбивкаВерсии = СтрРазделить(НомерПоследнегоПатча, ".");
	Если РазбивкаВерсии.Количество() <> 4 Тогда
		ВызватьИсключение "Не верный формат номера релиза";	
	КонецЕсли;
	
	ВерсияПатча = РазбивкаВерсии[РазбивкаВерсии.ВГраница()];
	РазбивкаВерсии.Удалить(РазбивкаВерсии.ВГраница());
	Объект.Наименование = СтрСоединить(РазбивкаВерсии, ".") + "." + (Число(ВерсияПатча) + 1);
	Объект.Код = Объект.Родитель.Код;
КонецПроцедуры

&НаСервере
Функция ПолучитьНомерПоследнегоПатча()
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Релизы.Наименование КАК Наименование
	|ИЗ
	|	Справочник.Релизы КАК Релизы
	|ГДЕ
	|	Релизы.Ссылка В ИЕРАРХИИ(&Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Наименование УБЫВ";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Родитель);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Наименование;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура Собрать(Команда)
	СобратьНаСервере();
КонецПроцедуры


&НаСервере
Процедура СобратьНаСервере()
	
	Объект.ДатаРелиза = ТекущаяДатаСеанса();
	Записать();
КонецПроцедуры

