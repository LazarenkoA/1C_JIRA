&НаКлиенте
Перем ИдентификаторФЗ, ИндексТекущегоЗадания;


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗадачиПопавшиеВРелиз.Параметры.УстановитьЗначениеПараметра("КонечныеСтатусы", СтрРазделить("Закрыт,Сделан,Предоставлено ПР", ","));
	ЗадачиПопавшиеВРелиз.Параметры.УстановитьЗначениеПараметра("ТекРелиз", Объект.Ссылка);
	ЗадачиПопавшиеВРелиз.Параметры.УстановитьЗначениеПараметра("ПрименитьОтбор", Ложь);	
	ЗадачиПопавшиеВРелиз.Параметры.УстановитьЗначениеПараметра("НачалоРелиза", Объект.ДатаНачала);	
	ЗадачиПопавшиеВРелиз.Параметры.УстановитьЗначениеПараметра("ОкончаниеРелиза", Объект.ДатаРелиза);	
	ЗадачиПопавшиеВРелиз.Параметры.УстановитьЗначениеПараметра("ТекРелизВыпущен", Объект.РелизВыпущен);	
	
	ДатаРелизаДоИзменений = Объект.ДатаРелиза;
	ИнициализироватьШаги();
КонецПроцедуры


&НаКлиенте
Процедура Собрать(Команда)
	Ошибки = Новый Массив();
	Если Элементы.грОшибка.Видимость Тогда
		Ошибки.Добавить("Существуют задачи запланированные, но не сделанные в текущем релизе");
	КонецЕсли;
	Если Объект.ДатаРелиза <> НачалоДня(ТекущаяДата()) Тогда
		Ошибки.Добавить("Необходимо изменить дату выпуска релиза. Дата должна быть равна текущей");
	КонецЕсли;
	Если ЭтаФорма.Модифицированность Тогда
		Ошибки.Добавить("Необходимо сохраниться");  // так надо, потому что при сохранении есть диалоговое окно вопроса
	КонецЕсли;
	Если Ошибки.Количество() > 0 Тогда
		Сообщить("Ошибки:
		|" + СтрСоединить(Ошибки, Символы.ПС));
		//Возврат;
	КонецЕсли;
	
	Ф = Новый Файл(ПутьККаталогуСРелизом);
	ОО = Новый ОписаниеОповещения("ПроверкаСуществованияЗавершение", ЭтотОбъект);
	Ф.НачатьПроверкуСуществования(ОО);
КонецПроцедуры

&НаКлиенте
Процедура ВыборКаталогаЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт 
	Если ВыбранныеФайлы <> Неопределено И ВыбранныеФайлы.Количество() = 1 Тогда
		ПутьККаталогуСРелизом	= ВыбранныеФайлы[0];
		ПродолжитьВыпуск();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаСуществованияЗавершение(Существует, ДополнительныеПараметры) Экспорт 
	Если Не Существует Тогда
		Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		ОО = Новый ОписаниеОповещения("ВыборКаталогаЗавершение", ЭтотОбъект);
		Диалог.Показать(ОО);
		Возврат;
	КонецЕсли;
	
	ПродолжитьВыпуск();
КонецПроцедуры


&НаКлиенте
Процедура ПродолжитьВыпуск()
	//ВыпускВерсииВJIRA();
	//ЭтаФорма.Прочитать();

	Элементы.Вкладки.ТекущаяСтраница = Элементы.грПроцесс;
	Элементы.Собрать.Доступность = Ложь;
	
	ПроверкаИЗапускСледующегоЗадания();
	ЭтаФорма.ПодключитьОбработчикОжидания("ПроверкаИЗапускСледующегоЗадания", 1, Ложь);
КонецПроцедуры



&НаКлиенте
Процедура ВыпускВерсииВJIRA()
	ЗадачиРелиза = ПолучитьЗадачи();
	ПереместитьВВерсию(ЗадачиРелиза, Объект.Ссылка);
	
	Данные = Новый Структура("released", Истина);
	ВзаимодействиеC_JIRA_КлиентСервер.ИзменитьДанныеВерсии(Формат(Объект.Код, "ЧГ="), Объект.Наименование, Данные);
КонецПроцедуры


&НаСервере
Функция ПолучитьЗадачи()
	Запрос = Новый Запрос(ЗадачиПопавшиеВРелиз.ТекстЗапроса);
	Запрос.Параметры.Вставить("КонечныеСтатусы", СтрРазделить("Закрыт,Сделан,Предоставлено ПР", ","));
	Запрос.Параметры.Вставить("ТекРелиз", Объект.Ссылка);
	Запрос.Параметры.Вставить("ПрименитьОтбор", Ложь);	
	Запрос.Параметры.Вставить("НачалоРелиза", Объект.ДатаНачала);	
	Запрос.Параметры.Вставить("ОкончаниеРелиза", Объект.ДатаРелиза);
	Запрос.Параметры.Вставить("ТекРелизВыпущен", Объект.РелизВыпущен);	
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Задача");
КонецФункции


&НаСервере
Процедура ИнициализироватьШаги()
	Задания.ПолучитьЭлементы().Очистить();
	
	СтрокаЛогаРодитель = Задания.ПолучитьЭлементы().Добавить();
	СтрокаЛогаРодитель.Комментарий = "Сборка расширения";
	СтрокаЛогаРодитель.АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор());
	СтрокаЛогаРодитель.Картинка = 0;
	СтрокаЛогаРодитель.Метод = "СборкаРасширений";
	НовСтр = СтрокаЛогаРодитель.ПолучитьЭлементы().Добавить();
	НовСтр.Комментарий = "Обновления расширения из репозитория";
	НовСтр.Метод = "ОбновленияРасширенияИзРепозитория";
	НовСтр.Картинка = 0;
	НовСтр = СтрокаЛогаРодитель.ПолучитьЭлементы().Добавить();
	НовСтр.Комментарий = "Создание временной файловой БД";
	НовСтр.Метод = "СоздатьВременнуюБД";
	НовСтр.Картинка = 0;
	НовСтр = СтрокаЛогаРодитель.ПолучитьЭлементы().Добавить();
	НовСтр.Комментарий = "Обновление расширения";
	НовСтр.Метод = "ОбновлениеРасширения";
	НовСтр.Картинка = 0;
	
	СтрокаЛогаРодитель = Задания.ПолучитьЭлементы().Добавить();
	СтрокаЛогаРодитель.Комментарий = СтрШаблон("Выгрузка актуальной конфигурации из хранилища ""%1""", Константы.АдресХранилища.Получить());
	СтрокаЛогаРодитель.АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, Новый УникальныйИдентификатор());
	СтрокаЛогаРодитель.Картинка = 0;
	СтрокаЛогаРодитель.Метод = "СтартВыгрузкиНаКлиенте";
	СтрокаЛогаРодитель.ОбратныйМетод = "ПолучитьКонфигурациюИзВременногоХранилища";
	
	НовСтр = СтрокаЛогаРодитель.ПолучитьЭлементы().Добавить();
	НовСтр.Комментарий = "Создание временной файловой БД";
	НовСтр.Метод = "СоздатьВременнуюБД";
	НовСтр.Картинка = 0;
	НовСтр = СтрокаЛогаРодитель.ПолучитьЭлементы().Добавить();
	НовСтр.Комментарий = "Получить конфигурацию из хранилища";
	НовСтр.Метод = "ПолучитьКонфигурациюИзХранилища";
	НовСтр.Картинка = 0;


	СтрокаЛогаРодитель = Задания.ПолучитьЭлементы().Добавить();
	СтрокаЛогаРодитель.Комментарий = "Обновление версии АЦК БУ";
	СтрокаЛогаРодитель.Картинка = 0;
	СтрокаЛогаРодитель.Метод = "ОбновлениеВерсииАЦКБУ";

	
	СтрокаЛогаРодитель = Задания.ПолучитьЭлементы().Добавить();
	СтрокаЛогаРодитель.Комментарий = "Обновление задач в JIRA";
	СтрокаЛогаРодитель.Картинка = 0;
	СтрокаЛогаРодитель.Метод = "ОбновлениеЗадачВ_JIRA";

КонецПроцедуры                                       

#Область ПроцессСборки

&НаКлиенте
Процедура СтартВыгрузкиНаКлиенте(Строка)
	СтартВыгрузки(Строка.АдресХранилища, Строка.ИдентификаторЗадания);
	ЭтаФорма.ПодключитьОбработчикОжидания("ПроверкаВыполненияЗаданий", 1, Ложь);	
КонецПроцедуры

&НаСервере
Процедура СтартВыгрузки(АдресХранилища, ИдентификаторЗадания)
	Парам = Новый Массив();  
	
	Парам.Добавить(АдресХранилища); 
	Парам.Добавить(Константы.АдресХранилища.Получить()); 
	Парам.Добавить(Константы.ПользовательХранилища.Получить()); 
	Парам.Добавить(Константы.ПарольХранилища.Получить()); 
	Парам.Добавить(-1); 
	ФЗ = ФоновыеЗадания.Выполнить("БФТ_ДлительныеОперацииСервер.ВыгрузитьКонфигурациюИзХранилища", Парам,, "Выгрузка конфигурации");
	ИдентификаторЗадания = ФЗ.УникальныйИдентификатор;
КонецПроцедуры

&НаСервере
Процедура СборкаРасширенийНаСервере(ИдентификаторЗадания)
	Парам = Новый Массив();  
	ФЗ = ФоновыеЗадания.Выполнить("БФТ_ДлительныеОперацииСервер.СборкаРасширений", Парам,, "Сборка расширений");
	ИдентификаторЗадания = ФЗ.УникальныйИдентификатор;
КонецПроцедуры


&НаКлиенте
Процедура СборкаРасширений(Строка)
	Попытка
		СборкаРасширенийНаСервере(Строка.ИдентификаторЗадания);
		ЭтаФорма.ПодключитьОбработчикОжидания("ПроверкаВыполненияЗаданий", 1, Ложь);	
	Исключение
		Строка.Картинка = 3;
		Строка.ТекстОшибкиКоротко = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Строка.ТекстОшибкиПодробно = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура ОбновлениеВерсииАЦКБУ(Строка)
	Попытка
		
		Строка.Картинка = 2;
	Исключение
		Строка.Картинка = 3;
		Строка.ТекстОшибкиКоротко = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Строка.ТекстОшибкиПодробно = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;

	
КонецПроцедуры


&НаКлиенте
Процедура ОбновлениеЗадачВ_JIRA(Строка)
	Попытка
		
		Строка.Картинка = 2;
	Исключение
		Строка.Картинка = 3;
		Строка.ТекстОшибкиКоротко = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Строка.ТекстОшибкиПодробно = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;

	
КонецПроцедуры



#КонецОбласти


&НаКлиенте
Процедура ПроверкаИЗапускСледующегоЗадания() Экспорт 
	
	Строки = Задания.ПолучитьЭлементы();
	
	Если ИндексТекущегоЗадания = Строки.Количество() Тогда
		ИндексТекущегоЗадания = 0;
		ЭтаФорма.ОтключитьОбработчикОжидания("ПроверкаВыполненияЗаданий");
		ЭтаФорма.ОтключитьОбработчикОжидания("ПроверкаИЗапускСледующегоЗадания");
		Элементы.Собрать.Доступность = Истина;
		Возврат;
	КонецЕсли;
	
	ТекСтрокаСЗаданием = Строки[ИндексТекущегоЗадания];
	Если Не ЗначениеЗаполнено(ТекСтрокаСЗаданием.Метод) Тогда
		ИндексТекущегоЗадания = ИндексТекущегоЗадания +1;
		Возврат;
	КонецЕсли;
		
	Если ТекСтрокаСЗаданием.Картинка = 1 Тогда
		Возврат; // Значит тек. задание еще выполняется	
	ИначеЕсли ТекСтрокаСЗаданием.Картинка = 2 Тогда 
		ИндексТекущегоЗадания = ИндексТекущегоЗадания +1;
		Возврат;    // Задание уже выполнелось, переходим к следующему
	ИначеЕсли ТекСтрокаСЗаданием.Картинка = 3 Тогда 
		ИндексТекущегоЗадания = 0;
		ЭтаФорма.ОтключитьОбработчикОжидания("ПроверкаВыполненияЗаданий");
		ЭтаФорма.ОтключитьОбработчикОжидания("ПроверкаИЗапускСледующегоЗадания");
		Элементы.Собрать.Доступность = Истина;
	Иначе
		ТекСтрокаСЗаданием.Картинка = 1;
		Выполнить(СтрШаблон("%1(ТекСтрокаСЗаданием)", ТекСтрокаСЗаданием.Метод));
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ПроверкаВыполненияЗаданий() Экспорт 
	ВсеВыполнено = Истина;
	Стр = Задания.ПолучитьЭлементы()[ИндексТекущегоЗадания];
	Если Не ЗначениеЗаполнено(Стр.ИдентификаторЗадания) Тогда
		ЭтаФорма.ОтключитьОбработчикОжидания("ПроверкаВыполненияЗаданий");	  
		Возврат;	
	КонецЕсли;
	
	Попытка
		Если Не ОбщегоНазначенияСервер.ЗаданиеВыполнено(Стр.ИдентификаторЗадания) Тогда
			ВсеВыполнено = Ложь;
			
			Стр.Картинка = 1;
			ДанныеФЗ = БФТ_ДлительныеОперацииСервер.ПолучитьДанныеФЗ(Стр.ИдентификаторЗадания, Ложь);
			Если ДанныеФЗ = Неопределено ИЛИ ТипЗнч(ДанныеФЗ) <> Тип("Массив") ИЛИ (ТипЗнч(ДанныеФЗ) = Тип("Массив") И ДанныеФЗ.Количество() = 0) Тогда
				Возврат;
			КонецЕсли;
			
			ПоследняяЗапись = ДанныеФЗ[ДанныеФЗ.ВГраница()];
			ВсеШагиФЗВыполнены = ПоследняяЗапись.ВыполненныеШаги.Количество() = Стр.ПолучитьЭлементы().Количество() ИЛИ Стр.ПолучитьЭлементы().Количество() = 0;
			Для Каждого ПодЗадание Из Стр.ПолучитьЭлементы() Цикл
				Стр.Картинка = 1;
				Если ПодЗадание.Метод = ПоследняяЗапись.ТекущийШаг Тогда
					ПодЗадание.Картинка = 1;	
				КонецЕсли;
				Если ПоследняяЗапись.ВыполненныеШаги.Найти(ПодЗадание.Метод) <> Неопределено ИЛИ ВсеШагиФЗВыполнены Тогда
					ПодЗадание.Картинка = 2;				
				КонецЕсли;
			КонецЦикла;
		Иначе
			ВсеШагиФЗВыполнены = Истина;    
			Для Каждого ПодЗадание Из Стр.ПолучитьЭлементы() Цикл
				ПодЗадание.Картинка = 2;				
			КонецЦикла;
		КонецЕсли;
		
		
		// Получаем данные ФЗ.
		Если ВсеШагиФЗВыполнены Тогда
			Стр.Картинка = 2;
			Если ЗначениеЗаполнено(Стр.ОбратныйМетод) Тогда
				Выполнить(СтрШаблон("%1(Стр.АдресХранилища)", Стр.ОбратныйМетод));  
			КонецЕсли;
		КонецЕсли;
	Исключение
		Стр.Картинка = 3;
		Стр.ТекстОшибкиКоротко = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Стр.ТекстОшибкиПодробно = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	//Иначе
	//	Стр.Картинка = 3;
	//	Стр.ТекстОшибкиКоротко = "Не найден идентификатор ФЗ";
	//КонецЕсли;
	Если ВсеВыполнено Тогда
		ЭтаФорма.ОтключитьОбработчикОжидания("ПроверкаВыполненияЗаданий");	  
	КонецЕсли;
	
	//ЭтаФорма.ОбновитьОтображениеДанных();
КонецПроцедуры


#region callback

&НаКлиенте
Процедура ПолучитьКонфигурациюИзВременногоХранилища(АдресХранилища)
	ПолучитьФайл(АдресХранилища, ПутьККаталогуСРелизом + СтрШаблон("\%1.cf", Объект.Наименование), Ложь);
КонецПроцедуры

#endregion

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	Запрос = Новый Запрос(ЗадачиПопавшиеВРелиз.ТекстЗапроса);
	Запрос.Параметры.Вставить("КонечныеСтатусы", СтрРазделить("Закрыт,Сделан,Предоставлено ПР", ","));
	Запрос.Параметры.Вставить("ТекРелиз", Объект.Ссылка);
	Запрос.Параметры.Вставить("ПрименитьОтбор", Истина);	
	Запрос.Параметры.Вставить("НачалоРелиза", Объект.ДатаНачала);	
	Запрос.Параметры.Вставить("ОкончаниеРелиза", Объект.ДатаРелиза);
	Запрос.Параметры.Вставить("ТекРелизВыпущен", Объект.РелизВыпущен);	
	
	Элементы.грОшибка.Видимость = Не Запрос.Выполнить().Пустой();
КонецПроцедуры


&НаСервере
Процедура ОтборЗадачНаСервере()
	ЗадачиПопавшиеВРелиз.Параметры.УстановитьЗначениеПараметра("ПрименитьОтбор", ОтборПрименен);	
КонецПроцедуры


&НаКлиенте
Процедура ОтборЗадач(Команда)
	ОтборПрименен = Не ОтборПрименен;
	ОтборЗадачНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиЗадачиВДругуюВерсию(Команда)
	ВыделенныеСтроки = Элементы.ЗадачиПопавшиеВРелиз.ВыделенныеСтроки;
	СписокЗадач = Новый Массив();
	Для Каждого Стр Из ВыделенныеСтроки Цикл
		Если ТипЗнч(Стр) = Тип("СправочникСсылка.Задачи") Тогда
		  СписокЗадач.Добавить(Стр);
		КонецЕсли;
	КонецЦикла;
	
	Если СписокЗадач.Количество() = 0 Тогда
		Сообщить("Выделите строки с задачами");
		Возврат;	
	КонецЕсли;
	
	Парам = Новый Структура("РелизыПозжеЧем, ТолькоНеВыпущенные", ТекущаяДата(), Истина);
	ДД = Новый Структура("СписокЗадач", СписокЗадач);
	ОткрытьФорму("Справочник.Релизы.ФормаВыбора", Парам,,,,, Новый ОписаниеОповещения("ПеренестиЗадачиВДругуюВерсиюЗавершение", ЭтотОбъект, ДД), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиЗадачиВДругуюВерсиюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Если Результат <> Неопределено Тогда
		ПереместитьВВерсию(ДополнительныеПараметры.СписокЗадач, Результат);	
		ЭтаФорма.Прочитать();
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПереместитьВВерсию(СписокЗадач, НоваяВерсия)
	Справочники.Релизы.ПереместитьВВерсию(СписокЗадач, НоваяВерсия);
КонецПроцедуры

&НаКлиенте
Процедура ЗадачиПопавшиеВРелизВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьВБраузере(Элемент, ВыбраннаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВБраузере(Элемент, ВыбраннаяСтрока)
	ТекСтрока = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	Если ТекСтрока <> Неопределено Тогда
		ВзаимодействиеC_JIRA_КлиентСервер.ОткрытьЗадачуВБраузере(ТекСтрока.Номер);
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ДатаРелизаПриИзмененииЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		АктуализироватьВерсии();
		ДанныеВерсий = ПолучитьВерсииДляОбновления(ДатаРелизаДоИзменений, Объект.Ссылка);
		
		// обновляем данные тек версии
		Данные = Новый Структура("releaseDate", Формат(Объект.ДатаРелиза, "ДФ=yyyy-MM-ddTHH:mm:ss.000+0300"));
		ВзаимодействиеC_JIRA_КлиентСервер.ИзменитьДанныеВерсии(Формат(Объект.Код, "ЧГ="), Объект.Наименование, Данные, Ложь);	
		
		// обновляем данные следующих версий
		РазницаДат = Объект.ДатаРелиза - ДатаРелизаДоИзменений;
		Для Каждого Версия Из ДанныеВерсий Цикл
			Данные = Новый Структура("releaseDate", Формат(Версия.ДатаРелиза + РазницаДат, "ДФ=yyyy-MM-ddTHH:mm:ss.000+0300"));
			Если ЗначениеЗаполнено(Версия.ДатаНачала) Тогда
				Данные.Вставить("startDate", Формат(Версия.ДатаНачала + РазницаДат, "ДФ=yyyy-MM-ddTHH:mm:ss.000+0300"));
			КонецЕсли;
			ВзаимодействиеC_JIRA_КлиентСервер.ИзменитьДанныеВерсии(Формат(Версия.ID, "ЧГ="), Версия.Наименование, Данные, Ложь);		
		КонецЦикла;
	КонецЕсли;
	ЭтаФорма.Модифицированность = Истина;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьВерсииДляОбновления(ДатаРелизаДоИзменений, ТекРелиз)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Релизы.ДатаРелиза КАК ДатаРелиза,
		|	Релизы.Код КАК ID,
		|	Релизы.Наименование КАК Наименование,
		|	Релизы.ДатаНачала КАК ДатаНачала
		|ИЗ
		|	Справочник.Релизы КАК Релизы
		|ГДЕ
		|	Релизы.Ссылка <> &ТекРелиз
		|	И Релизы.ДатаРелиза > &ДатаРелиза";
	
	Запрос.УстановитьПараметр("ДатаРелиза", ДатаРелизаДоИзменений);
	Запрос.УстановитьПараметр("ТекРелиз", ТекРелиз);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Ответ = Новый Массив();
	Пока Выборка.Следующий() Цикл
		Знч = Новый Структура("ДатаРелиза,ДатаНачала,ID,Наименование");
		ЗаполнитьЗначенияСвойств(Знч, Выборка);
		Ответ.Добавить(Знч);
	КонецЦикла;
	
	Возврат Ответ;
КонецФункции

	
&НаСервереБезКонтекста
Процедура АктуализироватьВерсии()
	Справочники.Релизы.ОбновитьВерсии();
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Если Не ЗначениеЗаполнено(Объект.ДатаРелиза) Тогда
		Отказ = Истина;
		Сообщить("Дата должна быть заполнена");
		Возврат;
	КонецЕсли;  
	
	Если Объект.ДатаРелиза > ДатаРелизаДоИзменений Тогда
		ПоказатьВопрос(Новый ОписаниеОповещения("ДатаРелизаПриИзмененииЗавершение", ЭтотОбъект), "Даты всех последующих релизов будет изменена", РежимДиалогаВопрос.ОКОтмена);
	Иначе
		Данные = Новый Структура("releaseDate", Формат(Объект.ДатаРелиза, "ДФ=yyyy-MM-ddTHH:mm:ss.000+0300"));
		ВзаимодействиеC_JIRA_КлиентСервер.ИзменитьДанныеВерсии(Формат(Объект.Код, "ЧГ="), Объект.Наименование, Данные, Ложь);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВкладкиПриСменеСтраницы(Элемент, ТекущаяСтраница)
	Если ТекущаяСтраница = Элементы.грReleaseNote Тогда
		СформироватьОписание();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура СформироватьОписание()
	Запрос = Новый Запрос(ЗадачиПопавшиеВРелиз.ТекстЗапроса);
	Запрос.Параметры.Вставить("КонечныеСтатусы", СтрРазделить("Закрыт,Сделан,Предоставлено ПР", ","));
	Запрос.Параметры.Вставить("ТекРелиз", Объект.Ссылка);
	Запрос.Параметры.Вставить("ПрименитьОтбор", Ложь);	
	Запрос.Параметры.Вставить("НачалоРелиза", Объект.ДатаНачала);	
	Запрос.Параметры.Вставить("ОкончаниеРелиза", Объект.ДатаРелиза);
	Запрос.Параметры.Вставить("ТекРелизВыпущен", Объект.РелизВыпущен);	
	
	Выборка = Запрос.Выполнить().Выбрать();
	ReleaseNote = "";
	Пока Выборка.Следующий() Цикл
		ReleaseNote = ReleaseNote + Строка(Выборка.Задача) + Символы.ПС;
		Если ЗначениеЗаполнено(Выборка.Родитель) Тогда
			Описание = СформироватьОписаниеПоЗадаче(Выборка.Родитель, Выборка.ЕстьКоммит);	
		Иначе
			Описание = СформироватьОписаниеПоЗадаче(Выборка.Задача, Выборка.ЕстьКоммит);	
		КонецЕсли;
		
		ReleaseNote = ReleaseNote + Символы.Таб + " - " + Описание + Символы.ПС + Символы.ПС;
	КонецЦикла;
КонецПроцедуры

&НаСервере
Функция СформироватьОписаниеПоЗадаче(Задача, ЕстьКоммит)
	Ответ = "";
	КонечныеСтатусы = СтрРазделить("Закрыт,Сделан,Предоставлено ПР", ",");
	Выборка = Справочники.Задачи.ВыбратьИерархически(Задача);
	РазработкаЗакрыта = КонечныеСтатусы.Найти(Задача.Статус) <> Неопределено;   
	ТестированиеЗакрыта = КонечныеСтатусы.Найти(Задача.Статус) <> Неопределено;
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.Тип.Родитель = Справочники.ТипыЗадач.Разработка Тогда
			РазработкаЗакрыта = КонечныеСтатусы.Найти(Выборка.Статус) <> Неопределено;
		КонецЕсли;
		Если Выборка.Тип.Родитель = Справочники.ТипыЗадач.Тестирование Тогда
			ТестированиеЗакрыта = КонечныеСтатусы.Найти(Выборка.Статус) <> Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	Если ЕстьКоммит И (Не РазработкаЗакрыта) Тогда
		Ответ = "Задача частично выложена разработкой";
	КонецЕсли;
	Если (Не ЕстьКоммит) И РазработкаЗакрыта Тогда
		Ответ = "По задаче не было внесено правок";
	КонецЕсли;
	Если ЕстьКоммит И РазработкаЗакрыта И ТестированиеЗакрыта Тогда
		Ответ = "Задача проверяна";
	КонецЕсли;
	Если ЕстьКоммит И РазработкаЗакрыта И Не ТестированиеЗакрыта Тогда
		Ответ = "Задача сделана разработкой, но не проверяна";
	КонецЕсли;
	
	Возврат Ответ;
КонецФункции


&НаКлиенте
Процедура ОбновитьСПолучениемИзменений(Команда)
	ИдентификаторФЗ = ЗапускЗадачНаСервере();
	Элементы.ДеревоЗадачВРаботеОбновитьСПолучениемИзменений.Доступность = Не ЗначениеЗаполнено(ИдентификаторФЗ);
	ЭтаФорма.ПодключитьОбработчикОжидания("ПроверкаСтатусаРегЗадания", "1", Ложь);
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗапускЗадачНаСервере()
	РЗ = РегламентныеЗадания.НайтиПредопределенное("ПолучениеЗадач");
	Если РЗ = Неопределено Тогда
		Сообщить("Не нашли регламентное задание");
		Возврат Неопределено;
	КонецЕсли;
	
	Если РЗ.Использование Тогда
		
		//проверика на выполнение в текущий момент
		Отбор = Новый Структура;
		Отбор.Вставить("Ключ", Строка(РЗ.УникальныйИдентификатор));
		Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);		
		МассивЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
		
		Если МассивЗаданий.Количество() = 0 Тогда 
			НаименованиеФоновогоЗадания = "Запуск вручную: "+ РЗ.Метаданные.Синоним;
			ФЗ = ФоновыеЗадания.Выполнить(РЗ.Метаданные.ИмяМетода, РЗ.Параметры, Строка(РЗ.УникальныйИдентификатор), НаименованиеФоновогоЗадания);
			Возврат ФЗ.УникальныйИдентификатор;
		Иначе
			Сообщить("Задание уже запущено");
		КонецЕсли;
	КонецЕсли;
КонецФункции


&НаКлиенте
Процедура ПроверкаСтатусаРегЗадания() Экспорт 
	Если ОбщегоНазначенияСервер.ЗаданиеВыполнено(ИдентификаторФЗ) Тогда
		Элементы.ДеревоЗадачВРаботеОбновитьСПолучениемИзменений.Доступность = Истина;
		ЭтаФорма.ОтключитьОбработчикОжидания("ПроверкаСтатусаРегЗадания");
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ЗаданияТекстОшибкиОткрытие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.Задания.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Сообщить(ТекущиеДанные.ТекстОшибкиПодробно);	
	КонецЕсли;
КонецПроцедуры

ИндексТекущегоЗадания = 0;