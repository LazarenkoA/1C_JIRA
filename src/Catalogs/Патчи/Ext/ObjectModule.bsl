/////////////// Защита модуля ///////////////
// @protect                                //
/////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
  Если ЭтоНовый() Тогда
    ЭтотОбъект.Дата = ТекущаяДатаСеанса();  
  КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
  ЭтотОбъект.ТекущийШаг = 0;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.Релизы") Тогда
		Если Не ДанныеЗаполнения.РелизВыпущен И Не ДанныеЗаполнения.РелизСобран Тогда
			СтандартнаяОбработка = Ложь;
			Сообщить("Патч можно создать на основании собраного или выпущенного релиза");
			Возврат;
		КонецЕсли;
		
		НомерПоследнегоПатча = ПолучитьНомерПоследнегоПатча(ДанныеЗаполнения.Ссылка);
		
		РазбивкаВерсии = СтрРазделить(НомерПоследнегоПатча, ".");
		Если РазбивкаВерсии.Количество() <> 4 Тогда
			ВызватьИсключение "Не верный формат номера релиза";	
		КонецЕсли;
		
		ВерсияПатча = РазбивкаВерсии[РазбивкаВерсии.ВГраница()];
		РазбивкаВерсии.Удалить(РазбивкаВерсии.ВГраница());
		
		// Заполнение шапки
		УстановитьНовыйКод();
		Владелец = ДанныеЗаполнения.Ссылка;  
		Наименование = СтрСоединить(РазбивкаВерсии, ".") + "." + (Число(ВерсияПатча) + 1); 
		БазовыйНомерРевизии = ПолучитьПоследнююРевизию(?(ДанныеЗаполнения.РелизВыпущен, ДанныеЗаполнения.ДатаРелиза, ДанныеЗаполнения.ДатаНачала));
		
		Справочники.Патчи.ЗагрузитьБазовыеНаборыИзменений(ЭтотОбъект);
	КонецЕсли;
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьНомерПоследнегоПатча(Релиз)
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	                |	ЕСТЬNULL(Патчи.Наименование, Релизы.Наименование) КАК Наименование
	                |ИЗ
	                |	Справочник.Релизы КАК Релизы
	                |		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Патчи КАК Патчи
	                |		ПО (Патчи.Владелец = Релизы.Ссылка)
	                |ГДЕ
	                |	Релизы.Ссылка = &Релиз
	                |
	                |УПОРЯДОЧИТЬ ПО
	                |	Наименование УБЫВ";
	
	Запрос.УстановитьПараметр("Релиз", Релиз);                            
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Наименование;
	КонецЕсли;
КонецФункции

Функция ПолучитьПоследнююРевизию(Дата)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	БФТ_ИзмененияРепозитория.НомерРевизии КАК НомерРевизии
		|ИЗ
		|	РегистрСведений.БФТ_ИзмененияРепозитория КАК БФТ_ИзмененияРепозитория
		|ГДЕ
		|	БФТ_ИзмененияРепозитория.Период <= &Дата
		|	И НЕ БФТ_ИзмененияРепозитория.ИзменениеКонфигурации
		|
		|УПОРЯДОЧИТЬ ПО
		|	БФТ_ИзмененияРепозитория.НомерРевизии УБЫВ";
	
	Запрос.УстановитьПараметр("Дата", Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		 Возврат ВыборкаДетальныеЗаписи.НомерРевизии;
	КонецЦикла;
КонецФункции


#КонецОбласти


#КонецЕсли