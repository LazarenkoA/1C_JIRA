
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОбновитьДерево();	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДерево()
	ВремТЗ = ОбщегоНазначенияСервер.ПолучитьЗадачиСКоммитамиПослеРелиза();
	ЗначениеВРеквизитФормы(ВремТЗ, "Список");
КонецПроцедуры


&НаКлиенте
Процедура _Закрыть(Команда)
	ЭтаФорма.Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьВБраузере(Элемент, ВыбраннаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВБраузере(Элемент, ВыбраннаяСтрока)
	ТекСтрока = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	Если ТекСтрока <> Неопределено И ТекСтрока.Свойство("Задача") Тогда
		ВзаимодействиеC_JIRA_КлиентСервер.ОткрытьЗадачуВБраузере(ОбщегоНазначенияСервер.ЗначениеРеквизитаОбъекта(ТекСтрока.Задача, "Код"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиЗадачу(Команда)
	ДанныеДляПереноса = Новый Соответствие();
	Для Каждого Стр Из Элементы.Список.ВыделенныеСтроки Цикл
		ДанныеСтроки = Элементы.Список.ДанныеСтроки(Стр);
		МассивЗадач = ДанныеДляПереноса[ДанныеСтроки.РелизКоммита];
		Если МассивЗадач = Неопределено Тогда
			МассивЗадач = Новый Массив();
			ДанныеДляПереноса.Вставить(ДанныеСтроки.РелизКоммита, МассивЗадач);
		КонецЕсли;
		
		МассивЗадач.Добавить(ДанныеСтроки.Задача); 
	КонецЦикла;
	
	Для Каждого Данные Из ДанныеДляПереноса Цикл
		Если Не ЗначениеЗаполнено(Данные.Ключ) Тогда
			Продолжить;
		КонецЕсли;
		
		СписокЗадач = ОбщегоНазначенияКлиентСервер.СвернутьМассив(Данные.Значение);
		ДобавитьВВерсиюНаСервере(СписокЗадач, Данные.Ключ);
		
		ОбновитьДанныеЗадачНаСервереВФоне(СписокЗадач);
	КонецЦикла;
	
	ОбновитьДерево();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОбновитьДанныеЗадачНаСервереВФоне(СписокЗадач)
	Номера = Новый Массив();
	Для Каждого Задача Из СписокЗадач Цикл
		Номера.Добавить(Задача.Код);	
	КонецЦикла;

	Параметры = Новый Массив();
	Параметры.Добавить(Номера);
	ФоновыеЗадания.Выполнить("БФТ_ДлительныеОперацииСервер.ОбновитьДанныеЗадачВФоне", Параметры,, "Обновление списка задач");
КонецПроцедуры


&НаСервереБезКонтекста
Процедура ДобавитьВВерсиюНаСервере(СписокЗадач, НоваяВерсия)
	Справочники.Релизы.ДобавитьВВерсию(СписокЗадач, НоваяВерсия);
КонецПроцедуры
