&НаКлиенте
Перем БылоИзменениеПорядкаСтрок, ИдентификаторФЗ;

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ОтобратьЗадачиПроизводства = Истина;
	
	СоздатьВкладкиБыстрогоФильтра();
	ОбновитьДевевьяНаСервере();
	УстановитьПараметрыДинСписка();
	
	ОтчетОбъект = Отчеты.СписаниеПоСотрудникам.Создать();
	СписаниеПоДням = ПодготовитьТабличныйДокумент(ОтчетОбъект.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных"),
																								ОтчетОбъект.КомпоновщикНастроек.ПолучитьНастройки());																			
КонецПроцедуры

Функция ПодготовитьТабличныйДокумент(СхемаКомпоновкиДанных, Настройки) Экспорт 
  Результат = Новый ТабличныйДокумент();
  
  ДанныеРасшифровки = Новый ДанныеРасшифровкиКомпоновкиДанных; // Создаем данные расшифровки 
  КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных; // Создаем компоновщик макета 
  МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, Настройки, ДанныеРасшифровки);
  
  // Компонуем результат
  ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
  ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
  
  ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
  ПроцессорВывода.УстановитьДокумент(Результат);	
  ПроцессорВывода.Вывести(ПроцессорКомпоновки);
  
  Возврат Результат;
КонецФункции


&НаСервере
Процедура УстановитьПараметрыДинСписка()
	НовыеЗадачи.Параметры.УстановитьЗначениеПараметра("ОтборЗадачПроизводства", ОтобратьЗадачиПроизводства);	
	НовыеЗадачи.Параметры.УстановитьЗначениеПараметра("ОтобратьНовые", ОтобратьНовые);	
	НовыеЗадачи.Параметры.УстановитьЗначениеПараметра("ГруппаРазработки", Справочники.Пользователи.Разработка);
	НовыеЗадачи.Параметры.УстановитьЗначениеПараметра("ГруппаАналитики", Справочники.Пользователи.Аналитика);
КонецПроцедуры

&НаСервере
Процедура СоздатьВкладкиБыстрогоФильтра()
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Пользователи.ФИО КАК ФИО,
		|	Пользователи.Наименование КАК Наименование
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.ОтслеживатьЗадачиНаРабочемСтоле";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 0 Тогда
		Элементы.Вкладки.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет 
	Иначе
		Элементы.Вкладки.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху; 
	КонецЕсли;
	
	Для Каждого Страница Из Элементы.Вкладки.ПодчиненныеЭлементы Цикл
		Если ТРег(Страница.Заголовок) = "Основная" Тогда
			Продолжить;
		КонецЕсли;
		Страница.Видимость = Ложь;
	КонецЦикла;
	
	Пока Выборка.Следующий() Цикл
		Имя = СтрЗаменить(Выборка.Наименование, ".", "_");
		СуществующийЭлемент = ЭтаФорма.Элементы.Найти(Имя);
		Если СуществующийЭлемент <> Неопределено Тогда
			СуществующийЭлемент.Видимость = Истина;
			Продолжить;	
		КонецЕсли;
		
		Вкладка = ЭтаФорма.Элементы.Добавить(Имя, Тип("ГруппаФормы"), Элементы.Вкладки);
		Вкладка.Вид = ВидГруппыФормы.Страница;
		Вкладка.Заголовок = СтрРазделить(Выборка.ФИО, " ")[0];
		
		СкопироватьЭлемент(Элементы.ИтогОценки, Вкладка.Заголовок, Вкладка);
		СкопироватьЭлемент(Элементы.ДеревоЗадачВРаботе, Вкладка.Заголовок, Вкладка);
		
	КонецЦикла;
КонецПроцедуры

&НаСервере
Процедура СкопироватьЭлемент(ИсходныйЭлемент, Префикс, НовыйРодитель) Экспорт 
	Если ИсходныйЭлемент = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	НовыйЭлемент = ЭтаФорма.Элементы.Добавить(СтрШаблон("%1_%2", Префикс, ИсходныйЭлемент.Имя), ТипЗнч(ИсходныйЭлемент), НовыйРодитель);
	НовыйЭлемент.Заголовок = ИсходныйЭлемент.Заголовок;
	Если ТипЗнч(ИсходныйЭлемент) = Тип("ТаблицаФормы") Тогда
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ИсходныйЭлемент, "ПутьКДанным,Заголовок,ШрифтЗаголовка,Шрифт,РежимВыделения,ТолькоПросмотр,ПутьКДаннымКартинкиСтроки,КартинкаСтрок,ИзменятьСоставСтрок,ИзменятьПорядокСтрок");
		НовыйЭлемент.УстановитьДействие("Выбор", "Подключаемый_ДеревоЗадачВРаботеВыбор"); 
		
		//ЗаполнитьЗначенияСвойств(ИсходныйЭлемент.КоманднаяПанель, НовыйЭлемент.КоманднаяПанель); 
		Для Каждого ПодЭлемент Из ИсходныйЭлемент.КоманднаяПанель.ПодчиненныеЭлементы Цикл
			СкопироватьЭлемент(ПодЭлемент, Префикс, НовыйЭлемент.КоманднаяПанель);	
		КонецЦикла;
	ИначеЕсли ТипЗнч(ИсходныйЭлемент) = Тип("ПолеФормы") Тогда
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ИсходныйЭлемент, "ПутьКДанным,Заголовок,ШрифтЗаголовка,Шрифт");
	ИначеЕсли ТипЗнч(ИсходныйЭлемент) = Тип("КнопкаФормы") И ЗначениеЗаполнено(ИсходныйЭлемент.ИмяКоманды) Тогда
		НовыйЭлемент.Картинка = ИсходныйЭлемент.Картинка;
		НовыйЭлемент.ИмяКоманды = ИсходныйЭлемент.ИмяКоманды;
		НовыйЭлемент.Отображение = ИсходныйЭлемент.Отображение;
	КонецЕсли;
		
	Если ОбщегоНазначенияКлиентСервер.НаличиеСвойстваУОбъекта(ИсходныйЭлемент, "ПодчиненныеЭлементы") Тогда
		Для Каждого ПодЭлемент Из ИсходныйЭлемент.ПодчиненныеЭлементы Цикл
			СкопироватьЭлемент(ПодЭлемент, Префикс, НовыйЭлемент);	
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьДевевьяНаСервере()

	Дерево = РеквизитФормыВЗначение("ДеревоЗадачВРаботе");
	ЗаполнитьДерево(Дерево);
	ЗначениеВРеквизитФормы(Дерево, "ДеревоЗадачВРаботе");
	
	ИтогОценки = Дерево.Строки.Итог("Оценка");
	
КонецПроцедуры


&НаКлиенте
Процедура ПриОткрытии(Отказ)
	БылоИзменениеПорядкаСтрок = Ложь;
	УправлениеВидимостьюЭлементов();
	ПроверкаСтатусаРегЗадания();
	ЭтаФорма.ПодключитьОбработчикОжидания("ПроверкаСтатусаРегЗадания", 2, Ложь); 
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаСтатусаРегЗадания()
	РучнойЗапуск = ЗначениеЗаполнено(ИдентификаторФЗ);
	Пояснение = ПроверкаСтатусаРегЗаданияНаСервере(РучнойЗапуск);	
	Элементы.ДеревоЗадачВРаботеОбновитьСПолучениемИзменений.Доступность = Не РучнойЗапуск;	
	
	Если РучнойЗапуск Тогда
		Попытка
			Если ОбщегоНазначенияСервер.ЗаданиеВыполнено(ИдентификаторФЗ) Тогда
				Прогресс = 0;
				ИдентификаторФЗ = Неопределено;
				
				ОбновитьДевевьяНаСервере();
			Иначе
				Прогресс = ПолучитьПрогресс();
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	УправлениеВидимостьюЭлементов();      
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверкаСтатусаРегЗаданияНаСервере(РучнойЗапускРегЗадания)
	Ответ = "";
	РЗ = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.ПолучениеЗадач);
	Если РЗ <> Неопределено И РЗ.ПоследнееЗадание <> Неопределено Тогда
		Если РучнойЗапускРегЗадания Тогда
			Ответ = "Выполняется обновление (ручной запуск)";	
		ИначеЕсли РЗ.ПоследнееЗадание.Состояние = СостояниеФоновогоЗадания.Активно Тогда
			Ответ = "Выполняется обновление (по расписанию)";	
		Иначе
			КонецПоследнегоЗадания = РЗ.ПоследнееЗадание.Конец;
			ПериодПовтораВТечениеДня = ?(РЗ.Расписание.ПериодПовтораВТечениеДня <> Неопределено, РЗ.Расписание.ПериодПовтораВТечениеДня, 0);
			СлВыполнение = ?(КонецПоследнегоЗадания <> Неопределено, КонецПоследнегоЗадания, ТекущаяДатаСеанса()) + ПериодПовтораВТечениеДня;
			
			РазницаМинут = (СлВыполнение - ТекущаяДатаСеанса()) / 60;
			Минут = Формат(Цел(РазницаМинут), "ЧДЦ=; ЧН=0; ЧГ=");
			Секунд = Формат(РазницаМинут % 1 * 60, "ЧДЦ=; ЧН=0; ЧГ=");
			
			Ответ = СтрШаблон("Запуск обновления через %1 мин. %2 сек", Минут, Секунд);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ответ;
КонецФункции

&НаКлиенте
Процедура УправлениеВидимостьюЭлементов()
	// Проверка корректности заполнения настроек подключения к JIRA
	Элементы.ДекорацияПредупреждение.Видимость = ЗначениеЗаполнено(ОбщегоНазначенияСервер.ПроверкаЗаполненияНастроекJIRA());
	Элементы.ДекорацияПредупреждениеГрафик.Видимость = Не НастройкиГрафикаЗаполнены();
	Элементы.ДекорацияПредупреждениеПочта.Видимость = Не НастройкиСервераРассылокЗаполнены();
	Элементы.ДекорацияПредупреждениеСВ.Видимость = Не СистемаВзаимодействия.ИнформационнаяБазаЗарегистрирована();
	
	КоличествоКонфликтующихДат = ОбщегоНазначенияСервер.КоличествоКонфликтующихДат();
	КоличествоКонфликтовПревышениеПоВерсиям = ОбщегоНазначенияСервер.КоличествоКонфликтовПревышениеПоВерсиям();
	Элементы.ДекорацияПредупреждениеКонфликтДат.Видимость = КоличествоКонфликтующихДат > 0 ИЛИ КоличествоКонфликтовПревышениеПоВерсиям > 0;
	Если КоличествоКонфликтующихДат > 0 И КоличествоКонфликтовПревышениеПоВерсиям > 0 Тогда
		Элементы.ДекорацияПредупреждениеКонфликтДат.Картинка = БиблиотекаКартинок.ВысокаяВажность;
	Иначе
		Элементы.ДекорацияПредупреждениеКонфликтДат.Картинка = БиблиотекаКартинок.Важность;
	КонецЕсли;
	Элементы.ДекорацияПредупреждениеКоммиты.Видимость = КоличествоКоммитов() > 0;
		
	Если Элементы.ДекорацияПредупреждениеКонфликтДат.Видимость Тогда
		//Элементы.ОткрытьФормуКонфликтов.Заголовок = СтрШаблон("Открыть форму конфликтов (конфликтов %1)", КоличествоКонфликтующихДат);
		Элементы.ОткрытьФормуКонфликтов.ЦветТекста = Новый Цвет(255, 0, 0);
	Иначе
		//Элементы.ОткрытьФормуКонфликтов.Заголовок = "Открыть форму конфликтов";
		Элементы.ОткрытьФормуКонфликтов.ЦветТекста = Новый Цвет(28, 85, 174);
	КонецЕсли;
	
	КоличествоРасхождениеВОценках = ОбщегоНазначенияСервер.ПолучитьРасхождениеВОценкахКоличество();
	Элементы.ДекорацияПредупреждениеОценка.Видимость = КоличествоРасхождениеВОценках > 0;
	Если Элементы.ДекорацияПредупреждениеОценка.Видимость Тогда
		Элементы.КорректировкаОценки.ЦветТекста = Новый Цвет(255, 0, 0);
	Иначе
		Элементы.КорректировкаОценки.ЦветТекста = Новый Цвет(28, 85, 174);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Функция КоличествоКоммитов()
	Возврат ОбщегоНазначенияСервер.ПолучитьЗадачиСКоммитамиПослеРелиза().Строки.Количество();
КонецФункции

&НаСервереБезКонтекста
Функция НастройкиГрафикаЗаполнены()
	Возврат Константы.РабочаяНеделя.Получить() > 0 И Константы.РабочийДень.Получить() > 0;	
КонецФункции

&НаСервереБезКонтекста
Функция НастройкиСервераРассылокЗаполнены()
	Возврат ЗначениеЗаполнено(Константы.СерверИсходящейПочтыSMTP.Получить()) И	
	ЗначениеЗаполнено(Константы.ПользовательИсходящейПочты.Получить()) И
	ЗначениеЗаполнено(Константы.ПарольЭлектроннойПочты.Получить()) И
	ЗначениеЗаполнено(Константы.ПортSMTP.Получить()) И
	ЗначениеЗаполнено(Константы.ИмяОтправителя.Получить()) И
	ЗначениеЗаполнено(Константы.АдресЭлектроннойПочты.Получить());
КонецФункции

&НаКлиенте
Процедура ОткрытьНастройкиJIRA(Команда)
	ОткрытьФорму("ОбщаяФорма.ФормаНастроекJIRA",,,,,, Новый ОписаниеОповещения("ОткрытьФормуКонстантЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкиГрафика(Команда)
	ОткрытьФорму("ОбщаяФорма.ФормаНастроекГрафикаРаботы",,,,,, Новый ОписаниеОповещения("ОткрытьФормуКонстантЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьНастройкиЭлектроннойПочты(Команда)
	ОткрытьФорму("ОбщаяФорма.ФормаНастроекЭлектроннойПочты",,,,,, Новый ОписаниеОповещения("ОткрытьФормуКонстантЗавершение", ЭтотОбъект), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонстантЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "НастройкиИзменены" ИЛИ 
		ИмяСобытия = "ИзмененияДатСохранено" ИЛИ 
		ИмяСобытия = "ОценкаСохранено" ИЛИ
		ИмяСобытия = "ОбновитьЭлементы" Тогда
		УправлениеВидимостьюЭлементов();	
	КонецЕсли;
	Если ИмяСобытия = "ОбновитьВкладки" Тогда
		СоздатьВкладкиБыстрогоФильтра();	
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДерево(Дерево)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Задачи.Код КАК Номер,
		|	Задачи.Наименование КАК Заголовок,
		|	Задачи.Оценка КАК Оценка,
		|	Задачи.Родитель.Код КАК НомерРодительскойЗадачи,
		|	Задачи.ПлановаяДатаЗавершения КАК ПлановаяДатаЗавершения,
		|	Задачи.Приоритет КАК Приоритет,
		|	Задачи.ДатаСоздания КАК ДатаСоздания,
		|	Задачи.Автор КАК Автор,
		|	Задачи.Исполнитель КАК Исполнитель,
		|	-(Приоритеты.Код - 5) КАК Картинка,
		|	Задачи.Статус КАК Статус,
		|	Задачи.Бюджет КАК БюджетПУ,
		|	Задачи.ДатуЗавершенияУстановилАвтор КАК ДатуЗавершенияУстановилАвтор,
		|	Задачи.Тип КАК Тип
		|ИЗ
		|	Справочник.Задачи КАК Задачи
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Приоритеты КАК Приоритеты
		|		ПО Задачи.Приоритет = Приоритеты.Ссылка
		|ГДЕ
		|	НЕ Задачи.Статус В (&СтатусыИсключения)
		|	И НЕ Задачи.ПометкаУдаления
		|	И (&Исполнитель = НЕОПРЕДЕЛЕНО
		|			ИЛИ Задачи.Исполнитель = &Исполнитель)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерРодительскойЗадачи";
	
	Запрос.УстановитьПараметр("СтатусыИсключения", СтрРазделить("Закрыт,Сделан,Предоставлено ПР", ","));
	//Запрос.УстановитьПараметр("ГруппаРазработка", Справочники.Пользователи.Разработка);
	//Запрос.УстановитьПараметр("ГруппаТестирования", Справочники.Пользователи.Тестирование);
	//Запрос.УстановитьПараметр("ОтборПоРазработке", БФильтр_Разработка);
	//Запрос.УстановитьПараметр("ОтборПоТестированию", БФильтр_Тестирование);
	//
	//Запрос.УстановитьПараметр("ТипРазработка", Справочники.ТипыЗадач.Разработка);
	//Запрос.УстановитьПараметр("ТипТестирование", Справочники.ТипыЗадач.Тестирование);    
	//
	//ТипыБезСабтасков = Новый Массив();
	//ТипыБезСабтасков.Добавить(Справочники.ТипыЗадач.НайтиПоКоду(3));  // Поручение
	//ТипыБезСабтасков.Добавить(Справочники.ТипыЗадач.НайтиПоКоду(10104)); // Аналитика
	//Запрос.УстановитьПараметр("ТипыБезСабтасков", ТипыБезСабтасков);
	
	Исполнитель = Справочники.Пользователи.НайтиПоНаименованию(СтрЗаменить(Элементы.Вкладки.ТекущаяСтраница.Имя, "_", "."));
	Запрос.УстановитьПараметр("Исполнитель", ?(ЗначениеЗаполнено(Исполнитель), Исполнитель, Неопределено));	
	//Если Элементы.Вкладки.ТекущаяСтраница = Элементы.МоиЗадачи Тогда
	//	Запрос.УстановитьПараметр("Исполнитель", Справочники.Пользователи.НайтиПоНаименованию("a.lazarenko"));	
	//Иначе 
	//	Запрос.УстановитьПараметр("Исполнитель", Неопределено);	
	//КонецЕсли;
	
	Выгрузка = Запрос.Выполнить().Выгрузить();
	Выгрузка.Индексы.Добавить("Номер");
	
	Дерево = РеквизитФормыВЗначение("ДеревоЗадачВРаботе");
	Дерево.Строки.Очистить();
	Для Каждого Стр Из Выгрузка Цикл
		Если Не ЗначениеЗаполнено(Исполнитель) И ЗначениеЗаполнено(Стр.НомерРодительскойЗадачи) Тогда
			Строка = Дерево.Строки.Найти(Стр.НомерРодительскойЗадачи, "Номер");
			Если Строка <> Неопределено Тогда
				НоваяСтрока = Строка.Строки.Добавить();
			Иначе
				Продолжить;
			КонецЕсли;
		Иначе
			НоваяСтрока = Дерево.Строки.Добавить();
		КонецЕсли;
		
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Стр);
	КонецЦикла;
	
	Дерево.Строки.Сортировать("Картинка Убыв, ПлановаяДатаЗавершения");
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачВРаботеНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	Выполнение = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачВРаботеОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачВРаботеПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачВРаботеПриИзменении(Элемент)
	//БылоИзменениеПорядкаСтрок = Истина;
	//ВизуализироватьИзменение();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачВРаботеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	// Вставить содержимое обработчика.
КонецПроцедуры       

&НаКлиенте
Процедура ВизуализироватьИзменение()
	Если БылоИзменениеПорядкаСтрок Тогда
		ЦветРамки	= Новый Цвет(255, 0, 0); 
		Элементы.Сохранить.КнопкаПоУмолчанию = Истина;
	Иначе
		ЦветРамки	= Новый Цвет(160, 160, 160); 
		Элементы.Обновить.КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Элементы.ДеревоЗадачВРаботе.ЦветРамки = ЦветРамки;
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьСписокВJIRA(Команда)
	Задачи = Новый Массив();
	Для Каждого Стр Из ДеревоЗадачВРаботе.ПолучитьЭлементы() Цикл
		Задачи.Добавить(Стр.Номер);	
	КонецЦикла;
	Если Задачи.Количество() > 0 Тогда
		НачатьЗапускПриложения(Новый ОписаниеОповещения("ОткрытьСписокВJIRAЗавершение", ЭтотОбъект), СтрШаблон("https://jira.bftcom.com/issues/?jql=key in(%1)", СтрСоединить(Задачи, ",")));
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьСписокВJIRA_Новые(Команда)
	Номера = ПолучитьНомераНаСервере();
	
	Если Номера.Количество() > 0 Тогда
		Блок = ОбщегоНазначенияКлиентСервер.РазбитьМассив(Номера, 100);
		Для Каждого НомераБлока Из Блок Цикл
			НачатьЗапускПриложения(Новый ОписаниеОповещения("ОткрытьСписокВJIRAЗавершение", ЭтотОбъект), СтрШаблон("https://jira.bftcom.com/issues/?jql=key in(%1)", СтрСоединить(НомераБлока, ",")));
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ПолучитьНомераНаСервере()
	Запрос = Новый Запрос();
	Запрос.Текст = НовыеЗадачи.ТекстЗапроса;
	Запрос.Параметры.Вставить("ОтборЗадачПроизводства", ОтобратьЗадачиПроизводства);	
	Запрос.Параметры.Вставить("ГруппаРазработки", Справочники.Пользователи.Разработка);
	Запрос.Параметры.Вставить("ГруппаАналитики", Справочники.Пользователи.Аналитика);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Номер");
КонецФункции

Процедура ОткрытьСписокВJIRAЗавершение(КодВозврата, ДополнительныеПараметры) Экспорт 
	
КонецПроцедуры


&НаКлиенте
Процедура Обновить(Команда)
	Если БылоИзменениеПорядкаСтрок Тогда
		Сообщить("Был изменен порядок строк, необходимо сохранить");
		Возврат;
	КонецЕсли;
	
	//Состояние("Запрашиваем задачи с JIRA");
	//ПолучитьЗадачиНаСервере();
	//Состояние("Обновляем дерево");
	ОбновитьДевевьяНаСервере();
	УправлениеВидимостьюЭлементов();
КонецПроцедуры


&НаКлиенте
Процедура ДеревоЗадачВРаботеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачВРаботеПередНачаломИзменения(Элемент, Отказ)
	// Вставить содержимое обработчика.
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	ПроверитьПоследовательностьПриоритетов();
	
	БылоИзменениеПорядкаСтрок = Ложь;
	ВизуализироватьИзменение();
КонецПроцедуры

&НаСервере
Процедура ПроверитьПоследовательностьПриоритетов()
	// Нам нужно определить, что порядок приоритетов не нарушен, по этому сравниваем список пользователя и отсортированый по приоритетам список 
	ДеревоИзмененное = РеквизитФормыВЗначение("ДеревоЗадачВРаботе");
	ДеревоЭталонное = РеквизитФормыВЗначение("ДеревоЗадачВРаботе");
	ДеревоЭталонное.Строки.Сортировать("Картинка Убыв");
	
	ОшибкиПоследовательности = Новый Массив();
	Для а = 0 По ДеревоИзмененное.Строки.Количество()-1 Цикл
		Если ДеревоИзмененное.Строки[а].Приоритет	<> ДеревоЭталонное.Строки[а].Приоритет Тогда
			ОшибкиПоследовательности.Добавить(СтрШаблон("Номер строки ""%1"" (задача ""%2"")", а+1, ДеревоИзмененное.Строки[а].Номер));	
		КонецЕсли;
	КонецЦикла;
	Если ОшибкиПоследовательности.Количество() > 0 Тогда
		ВызватьИсключение СтрШаблон("Нарушена последовательность приоритетов в строках:
		|%1", СтрСоединить(ОшибкиПоследовательности, Символы.ПС));	
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЗадачВРаботеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьВБраузере(Элемент, ВыбраннаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДеревоЗадачВРаботеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьВБраузере(Элемент, ВыбраннаяСтрока);
КонецПроцедуры


&НаКлиенте
Процедура ДеревоЗадачНовыеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ОткрытьВБраузере(Элемент, ВыбраннаяСтрока);
КонецПроцедуры


&НаКлиенте
Процедура ОткрытьВБраузере(Элемент, ВыбраннаяСтрока)
	ТекСтрока = Элемент.ДанныеСтроки(ВыбраннаяСтрока);
	Если ТекСтрока <> Неопределено Тогда
		ВзаимодействиеC_JIRA_КлиентСервер.ОткрытьЗадачуВБраузере(ТекСтрока.Номер);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПринятьЗадачу(Команда)
	Если Элементы.НовыеЗадачи.ТекущиеДанные <> Неопределено Тогда
		ДП = Новый Структура("ТекущиеДанные", Элементы.НовыеЗадачи.ТекущиеДанные);
		Парам = Новый Структура("Приоритет,Задача,БюджетПУ,ДатуЗавершенияУстановилАвтор", ДП.ТекущиеДанные.Приоритет, ДП.ТекущиеДанные.Номер, ДП.ТекущиеДанные.БюджетПУ, ДП.ТекущиеДанные.ДатуЗавершенияУстановилАвтор);
		Парам.Вставить("ДатаЗавершения",  ДП.ТекущиеДанные.ПлановаяДатаЗавершения);
		Парам.Вставить("Статус", "Новый");
		ОткрытьФорму("ОбщаяФорма.ФормаВводаИсходнойОценкиПоЗадаче", Парам,,,,, Новый ОписаниеОповещения("ПринятьЗадачуЗавершение", ЭтотОбъект, ДП), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПринятьЗадачуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	Перем ТекущиеДанные;
	
	Если ЗначениеЗаполнено(Результат) И ДополнительныеПараметры.Свойство("ТекущиеДанные", ТекущиеДанные) И ТекущиеДанные <> Неопределено Тогда
		ВзаимодействиеC_JIRA_КлиентСервер.ПринятьЗадачу(ТекущиеДанные.Номер, Результат);
		ВзаимодействиеC_JIRA_КлиентСервер.ОбновитьДанныеСабтасков(ТекущиеДанные.Номер, Результат);
		
		ОбновитьДевевьяНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВРаботеУРазработки(Команда)
	БФильтр_Разработка = Не БФильтр_Разработка;
	ОбновитьДевевьяНаСервере();	
КонецПроцедуры

&НаКлиенте
Процедура ВРаботеУТестирования(Команда)
	БФильтр_Тестирование = Не БФильтр_Тестирование;
	ОбновитьДевевьяНаСервере();
КонецПроцедуры



&НаСервере
Функция ПроверитьФильтр(Строка)
	Возврат (БФильтр_Тестирование И ПолучитьРодителя(Строка.Исполнитель) = ПредопределенноеЗначение("Справочник.Пользователи.Тестирование")
						ИЛИ БФильтр_Разработка И ПолучитьРодителя(Строка.Исполнитель) = ПредопределенноеЗначение("Справочник.Пользователи.Разработка")) 
				И СтрРазделить("ЗАКРЫТ,СДЕЛАН,ПРЕДОСТАВЛЕНО ПР", ",").Найти(Врег(Строка.Статус)) = Неопределено;	
КонецФункции


&НаСервереБезКонтекста
Функция ПолучитьРодителя(Ссылка)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Пользователи.Родитель КАК Родитель
		|ИЗ
		|	Справочник.Пользователи КАК Пользователи
		|ГДЕ
		|	Пользователи.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Родитель;	
	КонецЕсли;
	 
КонецФункции

&НаКлиенте
Процедура ОткрытьФормуКонфликтов(Команда)
	ОткрытьФорму("ОбщаяФорма.ФормаПревышениеСроков");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДиаграммы(Команда)
	МассивЗадач = Новый Массив();
	Для Каждого Стр Из ДеревоЗадачВРаботе.ПолучитьЭлементы() Цикл
		МассивЗадач.Добавить(Стр.Номер);	
		Для Каждого Стр2 Из Стр.ПолучитьЭлементы() Цикл
			МассивЗадач.Добавить(Стр2.Номер);	
		КонецЦикла;
	КонецЦикла;
	
	Парам = Новый Структура("Задачи", СтрСоединить(МассивЗадач, ","));
	Парам.Вставить("ТолькоСабтаски", Истина);
	ОткрытьФорму("ОбщаяФорма.ФормаДиаграмм", Парам, ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРегЗаданиеСейчас(Команда)
	ВыполнитьРегЗаданиеНаСервере();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВыполнитьРегЗаданиеНаСервере()
	ОбработкаРегламентныхЗаданий.ВыполнитьРегЗаданиеНаСервере("ПолучениеЗадач");	
КонецПроцедуры


&НаКлиенте
Процедура ИзменитьДанныеЗадачи(Команда)
	Префикс = Элементы.Вкладки.ТекущаяСтраница.Заголовок;
	Префикс = ?(Префикс = "Основная", "", Префикс + "_");
	
	ЭлементДерево = Элементы[Префикс + Элементы.ДеревоЗадачВРаботе.Имя];
	Если ЭлементДерево.ТекущиеДанные <> Неопределено Тогда
		ДП = Новый Структура("ТекущиеДанные", ЭлементДерево.ТекущиеДанные);
		Парам = Новый Структура("Приоритет", ДП.ТекущиеДанные.Приоритет);
		Парам.Вставить("ДатаЗавершения",  ДП.ТекущиеДанные.ПлановаяДатаЗавершения);
		Парам.Вставить("ОбновитьТолькоОценку", Истина);
		Парам.Вставить("БюджетПУ", ДП.ТекущиеДанные.БюджетПУ);
		Парам.Вставить("Статус", ДП.ТекущиеДанные.Статус);
		Парам.Вставить("ДатуЗавершенияУстановилАвтор", ДП.ТекущиеДанные.ДатуЗавершенияУстановилАвтор);
		Парам.Вставить("Задача", ДП.ТекущиеДанные.Номер);
		
		ОткрытьФорму("ОбщаяФорма.ФормаВводаИсходнойОценкиПоЗадаче", Парам,,,,, Новый ОписаниеОповещения("ИзменитьДатуЗавершения_Завершение", ЭтотОбъект, ДП), РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДатуЗавершения_Завершение(Результат, ДополнительныеПараметры) Экспорт
	Перем ТекущиеДанные;
	
	Если ЗначениеЗаполнено(Результат) И ДополнительныеПараметры.Свойство("ТекущиеДанные", ТекущиеДанные) И ТекущиеДанные <> Неопределено Тогда
		ВзаимодействиеC_JIRA_КлиентСервер.УстановитьОценкуВыполненияИВремяЗадачи(ТекущиеДанные.Номер, Результат);
		ВзаимодействиеC_JIRA_КлиентСервер.ОбновитьДанныеСабтасков(ТекущиеДанные.Номер, Результат);
		
		ОбновитьДевевьяНаСервере();
		РаботаСПочтовымиСообщениямиКлиентСервер.ОтправитьУведомлениеОСменеДаты(ТекущиеДанные.Номер);
	КонецЕсли;
	
КонецПроцедуры


&НаСервереБезКонтекста
Функция ЗапускЗадачНаСервере()
	РЗ = РегламентныеЗадания.НайтиПредопределенное("ПолучениеЗадач");
	Если РЗ = Неопределено Тогда
		Сообщить("Не нашли регламентное задание");
		Возврат Неопределено;
	КонецЕсли;
	
	Если РЗ.Использование Тогда
		
		//проверика на выполнение в текущий момент
		Отбор = Новый Структура;
		Отбор.Вставить("Ключ", Строка(РЗ.УникальныйИдентификатор));
		Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);		
		МассивЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
		
		Если МассивЗаданий.Количество() = 0 Тогда 
			НаименованиеФоновогоЗадания = "Запуск вручную: "+ РЗ.Метаданные.Синоним;
			ФЗ = ФоновыеЗадания.Выполнить(РЗ.Метаданные.ИмяМетода, РЗ.Параметры, Строка(РЗ.УникальныйИдентификатор), НаименованиеФоновогоЗадания);
			Возврат ФЗ.УникальныйИдентификатор;
		Иначе
			Сообщить("Задание уже запущено");
		КонецЕсли;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ОбновитьСПолучениемИзменений(Команда)
	ИдентификаторФЗ = ЗапускЗадачНаСервере();
	Элементы.ДеревоЗадачВРаботеОбновитьСПолучениемИзменений.Доступность = Не ЗначениеЗаполнено(ИдентификаторФЗ);
	ПроверкаСтатусаРегЗадания();
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПрогресс() 
	Отбор = Новый ОтборОбсужденийСистемыВзаимодействия();
	Отбор.Количество = 1;
	Отбор.ТекущийПользовательЯвляетсяУчастником = Истина;
	Отбор.НаправлениеСортировки = НаправлениеСортировки.Убыв;
	Обсуждения = СистемаВзаимодействия.ПолучитьОбсуждения(Отбор);
	
	Если Обсуждения.Количество() = 1 Тогда
		Обсуждение = Обсуждения[0];
		ОтборСообщений = Новый ОтборСообщенийСистемыВзаимодействия();
		ОтборСообщений.Обсуждение = Обсуждение.Идентификатор;
		ОтборСообщений.Количество = 1;
		ОтборСообщений.НаправлениеСортировки = НаправлениеСортировки.Убыв;

		Сообщения = СистемаВзаимодействия.ПолучитьСообщения(ОтборСообщений);
		Текст = ?(Сообщения.Количество() = 1, Строка(Сообщения[0].Текст), "");
		Возврат ?(Значениезаполнено(Текст), Число(Текст), 0);
	КонецЕсли;
	
	Возврат 0;    
КонецФункции


&НаКлиенте
Процедура НовыеЗадачиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ОткрытьВБраузере(Элемент, ВыбраннаяСтрока);
КонецПроцедуры


&НаКлиенте
Процедура ОтобратьЗадачиПроизводстваПриИзменении(Элемент)
	УстановитьПараметрыДинСписка();
КонецПроцедуры


&НаКлиенте
Процедура ВкладкиПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ОбновитьДевевьяНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура КорректировкаОценки(Команда)
	ОткрытьФорму("ОбщаяФорма.ФормаРазницаОценкиОсновнойИСабтасков");
КонецПроцедуры


&НаКлиенте
Процедура ОтобратьНовыеПриИзменении(Элемент)
	УстановитьПараметрыДинСписка();
	
	// При снятии флага сбрасываем признак этоновый у задач
	Если Не ОтобратьНовые Тогда
		ОбщегоНазначенияСервер.СнятьФлагЭтоНовая();
		ОчиститьСообщения();
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура РегистрацияВСистемеВзаимодействия(Команда)
	ОткрытьФорму("ОбщаяФорма.ФормаСистемаВзаимодействия");
КонецПроцедуры

&НаКлиенте
Процедура РаспределитьЗадачиПоВерсиям(Команда)
	ОткрытьФорму("Справочник.Релизы.Форма.ФормаРаспределенияЗадачПоВерсиям"); 
КонецПроцедуры

&НаКлиенте
Процедура КоммитыПослеВыпуска(Команда)
	ОткрытьФорму("ОбщаяФорма.ФормаАнализДополнительныхКоммитов");
КонецПроцедуры



РучнойЗапускРегЗадания = Ложь;